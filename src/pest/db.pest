WHITESPACE = _ { " " | "\t" | "\r" | "\n" }

file = _{ SOI ~ (model | query)* ~ EOI }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC|"_")* }
ident_list = { ident ~ ("," ~ ident)* ~ ","? }
number = @{ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }

model = { "model" ~ ident ~ "{" ~ field_list? ~ "}" }
field_list = { field ~ ("," ~ field)* ~ ","? }
field = { ident ~ ":" ~ type_ }

type_ = { ident ~ type_arg? ~ type_optional? }
type_arg = { "(" ~ number ~ ")" }
type_optional = { "?" }

query = { "query" ~ ident ~ "(" ~ ident_list? ~ ")" ~ "{" ~ qql_statement ~ "}" }

qql_statement = {
    qql_action ~ qql_quantifier ~ qql_selector_list
    ~ qql_where_clause?
}
qql_action = { ^"SELECT" | ^"UPDATE" | ^"DELETE" }
qql_quantifier = { ^"ONE" | ^"ALL" | qql_expr }
qql_selector = { ident ~ "(" ~ ident_list ~ ")" }
qql_selector_list = { qql_selector ~ ("," ~ qql_selector)* ~ ","? }
qql_where_clause = { "WHERE" ~ qql_expr }

qql_expr = { qql_expr_or }
qql_expr_or = { qql_expr_and ~ (qql_expr_or_op ~ qql_expr_and)* }
qql_expr_or_op = @{ ^"or" }
qql_expr_and = { qql_expr_eq ~ (qql_expr_and_op ~ qql_expr_eq)* }
qql_expr_and_op = @{ ^"and" }
qql_expr_eq = { qql_expr_ord ~ (qql_expr_eq_op ~ qql_expr_ord)* }
qql_expr_eq_op = @{ "==" | "!=" }
qql_expr_ord = { qql_expr_term ~ (qql_expr_ord_op ~ qql_expr_term)* }
qql_expr_ord_op = @{ "<=" | "<" | ">=" | ">" }
qql_expr_term = { qql_expr_factor ~ (qql_expr_term_op ~ qql_expr_factor)* }
qql_expr_term_op = @{ "+" | "-" }
qql_expr_factor = { qql_expr_unary ~ (qql_expr_factor_op ~ qql_expr_unary)* }
qql_expr_factor_op = @{ "*" | "/" | "%" }
qql_expr_unary = { qql_expr_unary_op? ~ qql_expr_primary }
qql_expr_unary_op = @{ ^"not" | "-" }
qql_expr_primary = _{ qql_expr_number | /*qql_expr_field |*/ qql_expr_ident }
qql_expr_number = { number }
// qql_expr_field = { ident ~ ("." ~ ident)? }
qql_expr_ident = { "#" ~ ident }