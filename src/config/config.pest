WHITESPACE = _ { " " | "\t" | "\r" | "\n" }

file = { SOI ~ (attribute)* ~ EOI }

attribute = {
    ident ~ (block | expr)
}

block = { "{" ~ (attribute)* ~ "}" }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC|"_")* }
number = @{ "-"? ~ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }

string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ string_char* }
string_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\"  |  "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}


expr = { call | string | number | path }
expr_list = { expr ~ ("," ~ expr)* }
call = { ident ~ "(" ~ expr_list? ~ ")" }

path_link = _{ ("." | "_" | ASCII_ALPHANUMERIC)+ }
path = @{ ("./" | "../" | "~/" | "/") ~ (path_link ~ "/")* ~ path_link? }